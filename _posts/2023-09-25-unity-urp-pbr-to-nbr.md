---
layout: post
title: "Unity URP Shader PBR è½¬ BlinnPhong"
subtitle: ''
author: "yqsas"
header-style: text
catalog: false
tags:
  - Unity
  - URP
---

## Unity URP Shader PBR è½¬ BlinnPhong

é¡¹ç›®ä¸­æœ‰ä¸€äº›ç¬¬ä¸‰æ–¹æˆ–è€…ASEç”Ÿæˆçš„Shaderä»£ç ï¼Œé»˜è®¤éƒ½æ˜¯ç”¨çš„PBRå…‰ç…§ï¼Œç„¶è€Œå¤§éƒ¨åˆ†æè´¨ä»…éœ€ç±»ä¼¼SimpleLitä¸­ä½¿ç”¨çš„BlinnPhongå…‰ç…§æ¨¡å‹å°±å¤Ÿç”¨äº†ï¼Œå°¤å…¶åœ¨ç§»åŠ¨ç«¯æˆ‘ä»¬å¯ä»¥è€ƒè™‘ä¼˜åŒ–ã€‚  
å¦‚ä½•ä½¿ç”¨æœ€å°‘çš„ä»£ç ï¼Œå°†PBRä½¿ç”¨çš„Metallicæµç¨‹çš„æè´¨å‚æ•°å¿«é€Ÿè½¬æ¢åˆ°BlinnPhongå…‰ç…§å‘¢ï¼Œä»Šå¤©ç ”ç©¶äº†ä¸€ä¼šå„¿æ•ˆæœè¿˜è¡Œï¼Œä¾›å¤§å®¶å‚è€ƒã€‚

### Shader

```
//BlinnPhongé¢å¤–åŠ å¼ºé«˜å…‰ï¼Œè´´è¿›PBRæ•ˆæœã€‚
half4 UniversalFragmentBlinnPhongAddSpecular(InputData inputData, half3 diffuse, half4 specularGloss, half smoothness, half smoothnessPower, half3 emission, half alpha)
{
    // To ensure backward compatibility we have to avoid using shadowMask input, as it is not present in older shaders
    #if defined(SHADOWS_SHADOWMASK) && defined(LIGHTMAP_ON)
    half4 shadowMask = inputData.shadowMask;
    #elif !defined (LIGHTMAP_ON)
    half4 shadowMask = unity_ProbesOcclusion;
    #else
    half4 shadowMask = half4(1, 1, 1, 1);
    #endif

    Light mainLight = GetMainLight(inputData.shadowCoord, inputData.positionWS, shadowMask);

    #if defined(_SCREEN_SPACE_OCCLUSION)
        AmbientOcclusionFactor aoFactor = GetScreenSpaceAmbientOcclusion(inputData.normalizedScreenSpaceUV);
        mainLight.color *= aoFactor.directAmbientOcclusion;
        inputData.bakedGI *= aoFactor.indirectAmbientOcclusion;
    #endif

    MixRealtimeAndBakedGI(mainLight, inputData.normalWS, inputData.bakedGI);

    half3 attenuatedLightColor = mainLight.color * (mainLight.distanceAttenuation * mainLight.shadowAttenuation);
    half3 diffuseColor = inputData.bakedGI + LightingLambert(attenuatedLightColor, mainLight.direction, inputData.normalWS);
    #if defined(_SPECGLOSSMAP) || defined(_SPECULAR_COLOR) || defined(_MASKMAP)
    half3 specularColor = LightingSpecular(attenuatedLightColor, mainLight.direction, inputData.normalWS, inputData.viewDirectionWS, specularGloss, smoothness) * (smoothnessPower * 16);
    #endif

    #ifdef _ADDITIONAL_LIGHTS
    uint pixelLightCount = GetAdditionalLightsCount();
    for (uint lightIndex = 0u; lightIndex < pixelLightCount; ++lightIndex)
    {
        Light light = GetAdditionalLight(lightIndex, inputData.positionWS, shadowMask);
    #if defined(_SCREEN_SPACE_OCCLUSION)
            light.color *= aoFactor.directAmbientOcclusion;
    #endif
        half3 attenuatedLightColor = light.color * (light.distanceAttenuation * light.shadowAttenuation);
        diffuseColor += LightingLambert(attenuatedLightColor, light.direction, inputData.normalWS);
    #if defined(_SPECGLOSSMAP) || defined(_SPECULAR_COLOR) || defined(_MASKMAP)
        specularColor += LightingSpecular(attenuatedLightColor, light.direction, inputData.normalWS, inputData.viewDirectionWS, specularGloss, smoothness) * (smoothnessPower * 16);
    #endif
    }
    #endif

    #ifdef _ADDITIONAL_LIGHTS_VERTEX
    diffuseColor += inputData.vertexLighting;
    #endif

    half3 finalColor = diffuseColor * diffuse + emission;

    #if defined(_SPECGLOSSMAP) || defined(_SPECULAR_COLOR) || defined(_MASKMAP)
    finalColor += specularColor;
    #endif

    return half4(finalColor, alpha);
}

//metallicã€smoothness å‡ä¸ºé‡‡æ ·åå·²æ··åˆå¥½çš„ç»“æœå€¼
half4 MetallicBlinnPhong(InputData inputData, half3 albedo, half3 specularColor, half metallic, half smoothness, half3 emission, half alpha)
{
    const half smoothnessPower = smoothness;
    smoothness = exp2(10 * smoothness + 1); //SimpleLitåšæ³•
    const half oneMinusReflectivity = OneMinusReflectivityMetallic(metallic);
    albedo = albedo * oneMinusReflectivity;
    half3 specular = lerp(kDieletricSpec.rgb, albedo, metallic) * specularColor;

    half4 color = UniversalFragmentBlinnPhongAddSpecular(
        inputData,
        albedo,
        half4(specular.rgb, 0),
        smoothness,
        smoothnessPower,
        emission,
        alpha);
    return color;
}
```

### å¯¹æ¯”

å¯¹æ¯”å›¾å·æ‡’å°±ä¸æ”¾äº†ğŸ«£ã€‚

æˆ‘ä»¬ä»¥ URP è‡ªå¸¦çš„åœ°å½¢ç€è‰²å™¨ `Universal Render Pipeline/Terrain/Lit` ä¸¾ä¸ªä¾‹å­ã€‚

- ä¿®æ”¹å‰ï¼š
```
half4 color = UniversalFragmentPBR(inputData,
                                       albedo,
                                       metallic,
                                       /* specular */ half3(0.0h, 0.0h, 0.0h),
                                       smoothness,
                                       occlusion,
                                       /* emission */ half3(0, 0, 0),
                                       alpha);
```

- ä¿®æ”¹å
```
half4 color = MetallicBlinnPhong(
                    inputData,
                    albedo,
                    1,
                    metallic,
                    smoothness,
                    half3(0, 0, 0),
                    alpha);
```



